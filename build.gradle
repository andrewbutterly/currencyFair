/*
 * This build file was auto generated by running the Gradle 'init' task
 * by 'andrew' at '27/05/16 10:11' with Gradle 2.9
 */

buildscript {
    repositories {
        mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		  }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.5.RELEASE")
		classpath("org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.0.1")
    }
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
apply plugin: 'spring-boot'
apply plugin: 'groovy'
apply plugin: "org.sonarqube"

jar {
  baseName = 'currencyfair-test-ab'
  version = '0.0.1'
}

repositories {
    mavenCentral()    
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
	integrationTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/integration-test/java')
		}
		resources.srcDir file('src/integration-test/resources')
	}
	loadTest {
		java {
			compileClasspath += main.output + test.output
			runtimeClasspath += main.output + test.output
			srcDir file('src/load-test/java')
		}
		resources.srcDir file('src/load-test/resources')
	}	
}

configurations {
	integrationTestCompile.extendsFrom testCompile
	integrationTestRuntime.extendsFrom testRuntime
	loadTestCompile.extendsFrom testCompile
	loadTestRuntime.extendsFrom testRuntime
}
	
dependencies {
  compile 					"org.springframework.boot:spring-boot-starter-web",
  		  					"org.springframework.boot:spring-boot-starter-websocket",
  		  					"org.springframework:spring-messaging",
  		  					"com.google.code.findbugs:jsr305:3.0.1",
      	  					"io.projectreactor:reactor-bus"
													
  testCompile 				"org.codehaus.groovy:groovy-all:2.4.3",
  							"org.spockframework:spock-core:1.0-groovy-2.4",
							"org.objenesis:objenesis:1.4",
							"cglib:cglib-nodep:3.1",
							
							"info.cukes:cucumber-java:1.2.4",
							"info.cukes:cucumber-spring:1.2.4",
							"info.cukes:cucumber-junit:1.2.4",
							'junit:junit:4.12',
							"org.apache.servicemix.bundles:org.apache.servicemix.bundles.spring-test:4.0.7.RELEASE_3",
							"org.springframework:spring-test:2.5.6.SEC03"
}



task integrationTest(type: Test) {
	testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
	outputs.upToDateWhen { false }
}

task loadTest(type: Test) {
	testClassesDir = sourceSets.loadTest.output.classesDir
	classpath = sourceSets.loadTest.runtimeClasspath
	outputs.upToDateWhen { false }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.9'
}